buildscript {
	repositories {
		jcenter()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
		classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
	}
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

version = "0.4.0-pre3"
group = "pl.asie.charset"
archivesBaseName = "Charset"

minecraft {
	version = "1.11-13.19.1.2199"

	runDir = "run"
	useDepAts = true
	makeObfSourceJar = false
	mappings = "snapshot_20161224"

	replace '@VERSION@', project.version
	replace '@MC_VERSION@', version
}

repositories {
	mavenCentral()
	maven { url "http://maven.amadornes.com/" }
	maven {
		name 'DVS1 Maven FS'
		url 'http://dvs1.progwml6.com/files/maven'
	}
	maven {
		name 'Sonatype OSSRH (Snapshots)'
		url 'https://oss.sonatype.org/content/repositories/snapshots/'
	}
	ivy {
		name 'asie dependency mirror'
		artifactPattern "http://asie.pl/javadeps/[module]-[revision](-[classifier]).[ext]"
	}
}

configurations {
	shadow
	compile.extendsFrom shadow
}

dependencies {
//	deobfCompile "MCMultiPart:MCMultiPart-experimental:1.3.0_87:universal"
	deobfCompile "mezz.jei:jei_1.11:4.1.1.207"
	shadow name: "libresample4j", version: "bc0a030"
	shadow "net.sandius.rembulan:rembulan-runtime:0.1-SNAPSHOT"
	shadow "net.sandius.rembulan:rembulan-compiler:0.1-SNAPSHOT"
	shadow "net.sandius.rembulan:rembulan-stdlib:0.1-SNAPSHOT"
//	shadow "org.nanohttpd:nanohttpd:2.3.1"
//	shadow "org.nanohttpd:nanohttpd-websocket:2.3.1"
}

sourceSets {
	main {
		resources {
			srcDirs += 'docs'
			srcDirs += 'lang'
		}
	}
}

processResources {
	// this will ensure that this task is redone when the versions change.
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	// replace stuff in mcmod.info, nothing else
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		
		// replace version and mcversion
		expand 'version':project.version, 'mcversion':project.minecraft.version
	}

	// copy everything else, thats not the mcmod.info
	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}

jar.dependsOn shadowJar
jar.enabled = false

shadowJar {
	archiveName = tasks.jar.archiveName
	configurations = [project.configurations.shadow]
//	relocate 'com.amadornes.jtraits', 'pl.asie.charset.lib.jtraits'
	relocate 'com.laszlosystems.libresample4j', 'pl.asie.charset.audio.libresample4j'
	relocate 'net.sandius.rembulan', 'pl.asie.charset.scripting.rembulan'
//	relocate 'fi.iki.elonen', 'pl.asie.charset.server.nanohttpd'
}

reobfJar {
	setJar(tasks.shadowJar.archivePath)
}
reobfJar.dependsOn shadowJar

task deobfJar(type: Jar) {
	from(sourceSets.main.output) {
		classifier = "deobf"
	}
}

artifacts {
	archives deobfJar
}
